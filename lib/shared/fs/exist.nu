module error {
  export def "unknown action type" [] {
    error make {
      msg: "unknown action type"
      help: "supported action types are panic, stdout-message, nothing"

    }

  }

  export def "unknown action level" [] {
    error make {
      msg: "unknown action level"
      help: "supported action levels are error, warning, info"

    }
  }

  export def "run-gen-cmd passed without gen-cmd" [ run_gen_cmd_flag_span: record<start: int, end: int> ] {
    error make {
      msg: "run-gen-cmd passed without gen-cmd"
      label: {
        text: "u passed the run-gen-cmd flag without gen-cmd"
        span: $run_gen_cmd_flag_span
      }

      help: "you need to pass a gen-cmd to run if the path is not exist"

    }
  }

}

use shared/fmt code;

def levels [] { ["error", "warning", "info"] };
def type [] { ["panic", "stdout-message", "nothing"] };
def return_types [] { ["bool", "path"] };

def success [ path: string, return_path: bool ] {
  if $return_path {
    return $path;

  } else {
    return true;

  }

}

export def "fs exist" [
  path: string # path to check if exist
  # if the path can generated by a command, pass the command here to used in the message
  --return-path(-r) = false # return the path if exist or null if not exist
  --run-gen-cmd(-G) = true # run the gen-cmd if the path is not exist
  --gen-cmd(-g): closure # a command to generate the path if not exist
  --action-type(-l): string@type = "stdout-message" # panic, stdout-message, nothing
  --action-level(-l): string@levels = "warning" # error, warning, info
  --stdlib(-s) # use the nu standard library log commands
]: nothing -> oneof<bool, nothing, string> {
  if not ( $path | path exists ) {
    if $run_gen_cmd {
      if ( $gen_cmd | is-empty ) {
        use error;
        error run-gen-cmd passed without gen-cmd (metadata $run_gen_cmd).span;

      }

      try {
        use std/log;
        log info "running gen-cmd, maybe this will take some time insha'Allah, or maybe this will run on the background";

        do $gen_cmd $path;

      } catch {|err|
        use ../log *;
        log error "gen-cmd failed" $"this is the error message:(char nl)( $err )";

      }
 
      if ( $path | path exists ) {
        return ( success $path $return_path );

      }

    }

    match $action_type {
      "panic" => {
        error make {
          msg: $"needed path: ($path) is not exist"
          label: {
            text: "this path is not exist"
            span: (metadata $path).span

          },

          help: $"try this command maybe will help you to make the path insha'Allah(char nl)(code ( $gen_cmd | to nuon --serialize))"

        }

      }
      "stdout-message" => {
        let message = $"needed path: ($path) is not exist";
        let help = $"try this command maybe will help you to make the path insha'Allah(char nl)(code ( $gen_cmd | to nuon --serialize))";

        if $stdlib {
          use std/log *;
          match $action_level {
            "error" => {
              error $message;

            }
            "warning" => {
              warning $message;

            }
            "info" => {
              info $message;

            }

          };

          info $help;

        } else {
          match $action_level {
            "error" => {
              use ../log "log error";
              log error $message $help;

            }
            "warning" => {
              use ../log "log warning";
              log warning $message $help;

            }
            "info" => {
              use ../log "log info";
              log info $message $help;

            }

          }

        };

      }
      "nothing" => {
        if $return_path {
          return null;

        } else {
          return false

        }

      }
      _ => {
        use error;
        error unknown action type;

      }

    }

  }

  return ( success $path $return_path );

}
